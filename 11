Процедура ОбработкаПроведения(Отказ, РежимПроведения)
  
	// Для каждого товара
	// узнать сколько этого товара есть на складе
	// если пользователь хочет продать больше чем есть, то 
	// 		запретить
	// если товара хватает
	// 		определить его стоимость
	// списать
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Товары.Товар КАК Товар,
	|	Остатки.Партия КАК Партия,
	|	Товары.Количество КАК КСписанию,
	|	ЕСТЬNULL(Остатки.КоличествоОстаток, 0) КАК Количество,
	|	ЕСТЬNULL(Остатки.СтоимостьОстаток, 0) КАК Стоимость
	|ИЗ
	|	Документ.РасходнаяНакладная.Номенклатура КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваров.Остатки(&Дата, Товар В (&Товары)) КАК Остатки
	|		ПО Товары.Товар = Остатки.Товар
	|ГДЕ
	|	Товары.Ссылка = &ЭтотДокумент
	|
	|УПОРЯДОЧИТЬ ПО
	|	Остатки.Партия.Дата ВОЗР
	|
	|ИТОГИ
	|	МИНИМУМ(КСписанию),
	|	СУММА(Количество)
	|ПО
	|	Товар
	|";
	Запрос.Параметры.Вставить("Дата", Дата);
	Запрос.Параметры.Вставить("ЭтотДокумент", Ссылка);
	Запрос.Параметры.Вставить("Товары", Номенклатура.ВыгрузитьКолонку("Товар"));
	
	ВыборкаТовары = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);	
	Пока ВыборкаТовары.Следующий() Цикл
		
		Если ВыборкаТовары.КСписанию > ВыборкаТовары.Количество Тогда
			Сообщить("Не хватает товара " + ВыборкаТовары.Товар);
			Отказ = Истина;
		КонецЕсли;
		
		КСписанию = ВыборкаТовары.КСписанию;
		
		ВыборкаПартии = ВыборкаТовары.Выбрать();
		Пока ВыборкаПартии.Следующий() Цикл
			
			Если КСписанию = 0 Тогда
				
				Прервать;
				
			ИначеЕсли КСписанию >= ВыборкаПартии.Количество Тогда
				
				ТекКоличество = ВыборкаПартии.Количество;
				ТекСтоимость = ВыборкаПартии.Стоимость;
				
			Иначе
				
				ТекКоличество = КСписанию;
				ТекСтоимость  = ?(ВыборкаПартии.Стоимость = 0, 0, ВыборкаПартии.Стоимость / ВыборкаПартии.Количество * КСписанию);
				
			КонецЕсли;
			
			Движение = Движения.ПартииТоваров.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период      = Дата;
			Движение.Партия      = ВыборкаПартии.Партия; 
			Движение.Товар       = ВыборкаПартии.Товар;
			Движение.Количество  = ТекКоличество;
			Движение.Стоимость   = ТекСтоимость;
			
			КСписанию = КСписанию - ТекКоличество;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Движения.ПартииТоваров.Записывать = Истина;
	
КонецПроцедуры
