// регистр ТоварыНаСкладах Расход
Движения.ТоварыНаСкладах.БлокироватьДляИзменения = Истина;
Движения.ТоварыНаСкладах.Очистить();
Для Каждого ТекСтрокаТовары Из Товары Цикл
	Движение = Движения.ТоварыНаСкладах.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
	Движение.Период = Дата;
	Движение.Склад = Склад;
	Движение.Товар = ТекСтрокаТовары.Товар;
	Движение.Количество = ТекСтрокаТовары.Количество;
КонецЦикла;
Движения.ТоварыНаСкладах.Записать();

Запрос = Новый Запрос;
Запрос.Текст = 
"ВЫБРАТЬ
|	Товары.Товар КАК Товар,
|	СУММА(Товары.Количество) КАК Количество
|ПОМЕСТИТЬ ВТ_Товары
|ИЗ
|	Документ.РасходнаяНакладная.Товары КАК Товары
|ГДЕ
|	Товары.Ссылка = &ЭтотДокумент
|СГРУППИРОВАТЬ ПО
|	Товары.Товар
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	Остатки.Товар,
|	Остатки.КоличествоОстаток КАК Количество
|ИЗ
|	РегистрНакопления.ТоварыНаСкладах.Остатки(
|			&МоментВремени,
|			Товар В
|				(ВЫБРАТЬ
|					ВТ_Товары.Товар
|				ИЗ
|					ВТ_Товары)) КАК Остатки
|ГДЕ
|	Остатки.КоличествоОстаток < 0
|";
Запрос.УстановитьПараметр("МоментВремени", Новый Граница(МоментВремени()));
Запрос.УстановитьПараметр("ЭтотДокумент", Ссылка);
РезультатЗапроса = Запрос.Выполнить();

Если НЕ РезультатЗапроса.Пустой() Тогда
	
	Отказ = Истина;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаСообщения = "Не хватает %Количество% единиц товара %Товар%";
		СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%Количество%", Выборка.Количество);
		СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%Товар%", Выборка.Товар);
		Сообщить(СтрокаСообщения);
	КонецЦикла;
	
КонецЕсли;
